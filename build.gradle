buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

allprojects {
    repositories {
        jcenter() {
            url = 'http://jcenter.bintray.com/'
        }
        maven {
            url 'http://repository.jboss.org/nexus/content/groups/public'
        }
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${version_major}.${version_minor}.${version_patch}"
group = "snownee.cuisine"
archivesBaseName = "Cuisine"

if (System.getenv().BUILD_NUMBER != null) {
    version += "-build${System.getenv().BUILD_NUMBER}"
}

sourceCompatibility = targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    maven {
        name = "jei"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = 'tterrag maven'
        url = "https://maven.tterrag.com/"
    }
    maven {
        name = "jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "tehnut"
        url = "https://tehnut.info/maven"
    }

    flatDir {
        dirs 'libs'
    }
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    mappings = mapping_version
    useDepAts = true // TODO Actually it could be bad practice

    replace "@VERSION_INJECT@", project.version
}

dependencies {
    // Why not deobfCompile? Since JEI 4.13.1, JEI uses stable_39 mapping, so we are on the same MCP version now.
    compile "mezz.jei:jei_${minecraft_version}:${jei_version}"

    deobfCompile "vazkii.patchouli:Patchouli:${patchi_version}"
    deobfCompile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${ct_version}") {
        exclude group: 'org.ow2.asm'
    }
    deobfCompile "CraftTweaker2:CraftTweaker2-API:${ct_version}"
    deobfCompile "CraftTweaker2:ZenScript:${ct_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:${top_version}") {
        exclude group: 'cofh'
    }
    deobfCompile "blusunrize:ImmersiveEngineering:0.12-92-559"
    compile files(
        'Nutrition-1.12.2-3.5.0.jar',
        'Baubles-1.12-1.5.2.jar',
        'Carry+On+MC1.12.2+v1.10.jar',
        'ToughAsNails-1.12.2-3.1.0.129-api.jar'
    )
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.d"
    exclude "**/*.exe"
    exclude "**/*.e"
    exclude "**/*.txt"
    manifest {
        attributes 'FMLAT': 'cuisine_at.cfg'
        attributes 'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}"
        attributes 'Timestamp': System.currentTimeMillis()
    }
}

javadoc {
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    options.links = [
            'https://docs.oracle.com/javase/8/docs/api/'
    ]
    options.tags = [
            "implSpec:a:Implementation Specification",
            "implNote:a:Implementation Note"
    ]
}
